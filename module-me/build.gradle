if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion build_versions.compile_sdk
    buildToolsVersion build_versions.build_tools
    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.study.androidnote.me"
        }
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode build_versions.versionCode
        versionName build_versions.versionName

        javaCompileOptions {
            annotationProcessorOptions {
                // 显式声明支持注解
                includeCompileClasspath true
                arguments = [AROUTER_MODULE_NAME: project.getName()]

            }
        }
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 设置了resourcePrefix值后，所有的资源名必须以指定的字符串做前缀，否则会报错
    // 但是resourcePrefix这个值只能限定xml里面的资源，并不能限定图片资源，所有图片资源仍然需要手动去修改资源名
    resourcePrefix "me_"

}

dependencies {
    api fileTree(dir: 'libs', excludes: ['butterknife-compiler-8.5.0.jar'], include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    annotationProcessor files('libs/butterknife-compiler-8.5.0.jar')
    annotationProcessor project(':arouter-compiler')
    api project(":lib-biz-common")
}
